// This is supporting software for CS321/CS322 Compilers and Language Design.
// Copyright (c) Portland State University.
//---------------------------------------------------------------------------
//
// MATTHEW O'BRIEN
// HOMEWORK 2. LL GRAMMAR 
// options { DEBUG_PARSER=true; }  /* Show debugging info */

PARSER_BEGIN(LLGrammar)
import java.io.*;

public class LLGrammar {
  public static void main(String [] args) {
    if (args.length < 1) {
      System.out.println("Need a file name as command-line argument.");
      return;
    } 
    try {
      FileReader input = new FileReader(args[0]);
      new LLGrammar(input).Program();
      input.close();
    } catch (TokenMgrError e) {
      System.err.println(e);
    } catch (Exception e) {
      System.err.println(e);
    }
  }
}
PARSER_END(LLGrammar)

//
// LEXER SECTION ---------------------------------------------------------------
//
// A simplified version. Error detections are not included.
// 
SKIP:  /* Whitespace */
{ 
  " " | "\t" | "\r" | "\n" 
}

SKIP:  /* Comments -- single-line form only */
{
  <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> 
}

TOKEN:  /* Keywords */
{
  "class" | "extends" | "static" | "public" | "main" | "void" | "boolean"
| "int" | "double" | "String" | "true" | "false" | "new" | "this" | "if"
| "else" | "while" | "return" | "System" | "out" | "println"
}

TOKEN:  /* Literals and IDs */
{
  <#DIGIT:  ["0"-"9"]>
| <#LETTER: ["A"-"Z"]|["a"-"z"]>
| <INTLIT:  "0"|["1"-"9"](<DIGIT>)*> 	// decimal form only
| <DBLLIT:  (<DIGIT>)+"."(<DIGIT>)*|(<DIGIT>)*"."(<DIGIT>)+> 
| <STRLIT:  ("\"" (~["\"","\r","\n"])* "\"")>
| <ID:      <LETTER> (<LETTER>|<DIGIT>)*>
}

TOKEN:  /* Operators and delimiters */
{
  "+" | "-" | "*" | "/" | "&&" | "||" | "!" | "==" | "!=" | "<" | "<=" 
| ">" | ">=" |  "=" | ";" | "," | "." | "(" | ")" | "[" | "]" | "{" | "}"
}

//
// PARSER SECTION ---------------------------------------------------------------
//

// Program -> {ClassDecl}
//
void Program(): {}
{
  (ClassDecl())* <EOF>
}

// ClassDecl -> "class" <ID> ["extends" <ID>] "{" {VarDecl} {MethodDecl} "}"
//
void ClassDecl(): {}
{
  "class" <ID> ["extends" <ID>] "{" (VarDecl())* (MethodDecl())* "}"
}



//Test Implementation
void MethodDecl(): {}
{
  "public" MethodDecl1() "{"  (LOOKAHEAD(2) VarDecl())* (Stmt())* "}"
}
void MethodDecl1(): {}
{
  ExtType() <ID> "(" [Param() ("," Param())*] ")"
| "static" "void" "main" "(" "String" "[" "]" <ID> ")"
}

// Param -> Type <ID> 
//
void Param(): {} 
{
  Type() <ID> 
}

// VarDecl -> Type <ID> ["=" InitExpr] ";"
//  
void VarDecl(): {}
{
  Type() <ID>  ["=" InitExpr()] ";"
}

// ExtType -> Type | "void"
//
void ExtType(): {}
{
  Type() | "void"
}

// Type -> BasicType 
//      |  BasicType "[" "]" 
//      |  <ID>
//
void Type(): {}
{
  BasicType() ("[" "]")?
| <ID>
}


// BasicType -> "int" | "double" | "boolean"
//
void BasicType(): {}
{
  "int" | "double" | "boolean"
}

// Stmt -> "{" {Stmt} "}"
//      |  ExtId "(" [Args] ")" ";"
//      |  Lvalue "=" InitExpr ";"
//      |  "if" "(" Expr ")" Stmt ["else" Stmt]
//      |  "while" "(" Expr ")" Stmt
//      |  "System" "." "out" "." "println" "(" [PrArg] ")" ";"
//      |  "return" [Expr] ";"
//
void Stmt(): {}
{
  "{" (Stmt())* "}"
| ExtId() MoarStuff()
| "if" "(" Expr() ")" Stmt() ([LOOKAHEAD(2) "else" Stmt()])
| "while" "(" Expr() ")" Stmt()
| "System" "." "out" "." "println" "(" [ PrArg() ] ")" ";"  
| "return" [Expr()] ";"
}

//Added for test cases
void MoarStuff(): {}
{
  "(" [Args()] ")" ";"
| ["[" Expr() "]"] "=" InitExpr() ";"
} 

// Args -> Expr {"," Expr}
//
void Args(): {}
{
  Expr() ("," Expr())*
}

// PrArg -> Expr | <STRLIT>
//
void PrArg(): {}
{
  Expr() | <STRLIT>
}

// InitExpr -> "new" BasicType "[" <INTLIT> "]" 
//          |  "new" <ID> "(" ")"
//          |  Expr 
//

//Test Implementation for InitExpr()
void InitExpr(): {}
{
  "new" InitExpr1() 
| Expr()
}

void InitExpr1(): {}
{
  BasicType() "[" <INTLIT> "]"
| <ID> "(" ")"
}



// Expr -> Expr BinOp Expr 
//      |  UnOp Expr
//      |  "(" Expr ")" 
//      |  ExtId "(" [Args] ")"
//      |  Lvalue
//      |  Literal
//

//Operations built to remove left hand recursion
void Expr(): {}
{
  Expr1() (BinOp1() Expr1())*
}

void Expr1(): {}
{
  Expr2() (BinOp2() Expr2())*
}

void Expr2(): {}
{
  Expr3() (BinOp3() Expr3())*
}

void Expr3(): {}
{
  Expr4() (BinOp4() Expr4())*
}

void Expr4(): {}
{
  Expr5() (BinOp5() Expr5())*
}

void Expr5(): {}
{
  (UnOp())* Expr6()
}

void Expr6(): {}
{
//  Expr7() ((LOOKAHEAD(3) ExtId() |  ExtId() "(" [Args()] ")")*)
  ExtId() (Expr7())* | Expr8() 
}

void Expr7(): {}
{
  "[" Expr() "]"
| "(" [Args()] ")"
}


void Expr8(): {}
{
  Literal()
| ("(" Expr() ")")
}


// ExtId -> ["this" "."] <ID> {"." <ID>} 
//
void ExtId(): {}
{
  ["this" "."] <ID> ("." <ID>)* //("[" Expr() "]" )?
}


// Literal -> <INTLIT> | <DBLLIT> | "true" | "false"
//
void Literal(): {}
{
  <INTLIT> | <DBLLIT> | "true" | "false"
}


// BinOp -> "+" | "-" | "*" | "/" | "&&" | "||" 
//       |  "==" | "!=" | "<" | "<=" | ">" | ">=" 
//
void BinOp1(): {}
{
  "||"  
}
void BinOp2(): {}
{
  "&&"
}
void BinOp3(): {}
{
  "==" | "!=" | "<" | "<=" | ">" | ">="
}
void BinOp4(): {}
{
  "+" | "-"
}
void BinOp5(): {}
{
  "*" | "/"
}

// UnOp -> "-" | "!"
//
void UnOp(): {}
{
  "-" | "!"
}
